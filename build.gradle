plugins {
    id 'java'
	id 'com.google.protobuf' version '0.9.4'
    id 'idea'
    id 'application'
    id 'visual-studio'
    id 'maven-publish'
	id "io.github.uoxx3.project-environment" version "1.0.1"
}

group 'com.nikhilm'

java {
	sourceCompatibility = JavaVersion.VERSION_11
}

def grpcVersion = '1.65.1'
def protobufVersion = '3.25.3'
def baseVersion = '3.9.4'
def baseGroupId = 'io.github.adempiere'
def privateDependencyBaseVersion = "adempiere-3.9.4"

def javaMainClass = "org.spin.report_engine.server.ReportEngineServer"
def descriptorFile = "adempiere-report-engine-service.dsc"

startScripts.enabled = false


application {
	mainClass.set(javaMainClass)
}
jar {
	manifest {
		attributes(
			"Main-Class": javaMainClass
		)
	}
}

run {
	doFirst {
		// Set environment variables by .env project file
		environment "ADEMPIERE_APPS_TYPE", projectEnv.get("ADEMPIERE_APPS_TYPE", null) ?: "wildfly"
		environment "TZ", projectEnv.get("TZ", null) ?: "America/Caracas"
	}
	args = [
		"resources/env.yaml"
	]
}

repositories {
	mavenLocal()
    mavenCentral()
	maven {
		// Custom GitHub Repository (ADempiere by Default)
		url = findProperty("deployRepository") ?: System.properties['deploy.repository'] ?:
			projectEnv.get("GITHUB_DEPLOY_REPOSITORY", null) ?: System.getenv("GITHUB_DEPLOY_REPOSITORY") ?:
			"https://maven.pkg.github.com/adempiere/adempiere"
		credentials {
			// project property, system property, .env project file, system enviroment variable
			username = findProperty("deployUsername") ?: System.properties['deploy.user'] ?:
				projectEnv.get("GITHUB_DEPLOY_USER", null) ?: System.getenv("GITHUB_DEPLOY_USER")
			password = findProperty("deployToken") ?: System.properties['deploy.token'] ?:
				projectEnv.get("GITHUB_DEPLOY_TOKEN", null) ?: System.getenv("GITHUB_DEPLOY_TOKEN")
		}
	}
	maven {
		// Adempiere GitHub Organization
		url = "https://maven.pkg.github.com/adempiere/adempiere"
		credentials {
			// project property, system property, .env project file, system enviroment variable
			username = findProperty("deployUsername") ?: System.properties['deploy.user'] ?:
				projectEnv.get("GITHUB_DEPLOY_USER", null) ?: System.getenv("GITHUB_DEPLOY_USER")
			password = findProperty("deployToken") ?: System.properties['deploy.token'] ?:
				projectEnv.get("GITHUB_DEPLOY_TOKEN", null) ?: System.getenv("GITHUB_DEPLOY_TOKEN")
		}
	}
}

protobuf {
	protoc {
		artifact = "com.google.protobuf:protoc:${protobufVersion}"
	}
	plugins {
		grpc {
			artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
		}
	}
	generateProtoTasks {
		all()*.plugins {
			grpc {}
		}
		all().configureEach { task ->
			// If true, will generate a descriptor_set.desc file under
			// task.outputBaseDir. Default is false.
			// See --descriptor_set_out in protoc documentation about what it is.
			task.generateDescriptorSet = true

			// Allows to override the default for the descriptor set location
			task.descriptorSetOptions.path = "${projectDir}/build/descriptors/${descriptorFile}"
			// task.descriptorSetOptions.path = "${projectDir}/build/descriptors/adempiere-report-engine-service.dsc"

			// If true, the descriptor set will contain line number information
			// and comments. Default is false.
			task.descriptorSetOptions.includeSourceInfo = true

			// If true, the descriptor set will contain all transitive imports and
			// is therefore self-contained. Default is false.
			task.descriptorSetOptions.includeImports = true
		}
	}
}

sourceSets {
	main {
		java {
			srcDirs 'build/generated/source/proto/main/grpc'
			srcDirs 'build/generated/source/proto/main/java'
			srcDirs 'src/main/proto'
		}
		resources {
			srcDirs 'src/main/java'
			include 'org/spin/base/version.properties'
		}
	}
}

// Copy proto descriptor another folder
allprojects {
	task copyDescriptor(type: Copy) {
		from "${projectDir}/build/descriptors/${descriptorFile}"
		into "${projectDir}/docker-compose/envoy/definitions"
		// rename { "${descriptorFile}" }
	}

	tasks.matching {
		it.name.startsWith('compileJava')
	}.each { task ->
		task.finalizedBy copyDescriptor
	}
}

task AdempiereReportEngineServer(type: CreateStartScripts) {
	mainClass = javaMainClass
	applicationName = 'adempiere-report-engine-service-server'
	outputDir = new File(project.buildDir, 'tmp')
	classpath = jar.outputs.files + project.configurations.runtimeClasspath
}

applicationDistribution.into('bin') {
	from(AdempiereReportEngineServer)
	fileMode = 0755
}

//	Create release for project
task createRelease(type: Copy) {
   dependsOn build
   from file("$buildDir/distributions/")
   into file("$buildDir/release/")
   //destinationDir(file('build/release/'))
   doLast {
        file('build/release/')
        	.listFiles({file -> file.isFile()} as FileFilter).sort()
        	.each { File file ->
            ant.checksum file: file
        }
   	}
}

dependencies {
	implementation fileTree(
        dir: 'dependencies',
        include: [
            '*.jar'
        ]
    )
    implementation "io.grpc:grpc-netty-shaded:${grpcVersion}"
    implementation "io.grpc:grpc-alts:${grpcVersion}"
	implementation "io.netty:netty-handler:4.1.107.Final"
    implementation "io.grpc:grpc-protobuf:${grpcVersion}"
    implementation "io.grpc:grpc-stub:${grpcVersion}"
    implementation "io.grpc:grpc-netty:${grpcVersion}"
    // https://mvnrepository.com/artifact/com.google.protobuf/protobuf-java
	implementation "com.google.protobuf:protobuf-java:${protobufVersion}"
	implementation "com.google.protobuf:protobuf-java-util:${protobufVersion}"

	// ADempiere External Libraries
	implementation 'io.vavr:vavr:0.10.4'

	//	ADempiere Core
	implementation "${baseGroupId}:base:${baseVersion}"

	// ADempiere Projects with additional features
	//	ADempiere gRPC Utils Base (https://central.sonatype.com/artifact/io.github.adempiere/adempiere-grpc-utils)
	implementation "${baseGroupId}:adempiere-grpc-utils:1.5.7"
	// AWS3 Connector to use as File Storagee (https://central.sonatype.com/artifact/io.github.adempiere/adempiere-s3-connector)
	implementation "${baseGroupId}:adempiere-s3-connector:1.0.7"
	implementation "${baseGroupId}:adempiere-jwt-token:1.0.4"
	implementation "io.jsonwebtoken:jjwt-api:0.12.6"
	implementation "io.jsonwebtoken:jjwt-impl:0.12.6"
	implementation "io.jsonwebtoken:jjwt-jackson:0.12.6"

	//	Others
	compileOnly 'org.apache.tomcat:annotations-api:6.0.53'
	implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.17.0'
	implementation "org.apache.poi:poi-ooxml:5.2.5"
}
